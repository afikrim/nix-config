#!/usr/bin/env zsh

source "$HOME/.zsh/dev/helpers.zsh"

: "${MEKARI_DEV_DIR:=${MEKARI_DEV_DIR:-$HOME/Development/mekari}}"
: "${MEKARI_WORKTREES_DIR:=$MEKARI_DEV_DIR/worktrees}"

alias gtwacc='dev_mekari::accounting_worktree'
alias gtwqb='dev_mekari::quickbook_worktree'

dev_mekari::accounting_worktree() {
  local branch=$1
  shift

  if [[ -z "$branch" ]]; then
    echo "Usage: gtwacc <new-branch> [base-branch] [--code]"
    return 1
  fi

  local base_branch="v_next"
  local open_code=false

  while [[ $# -gt 0 ]]; do
    case "$1" in
      --code)
        open_code=true
        ;;
      *)
        base_branch="$1"
        ;;
    esac
    shift
  done

  local worktree_dir
  worktree_dir=$(dev::worktree_path "$MEKARI_WORKTREES_DIR" "$branch")
  local vscode_dir="$worktree_dir/.vscode"
  local env_file="$vscode_dir/.env"
  local mcp_file="$vscode_dir/mcp.json"

  dev::log "Creating accounting_service worktree '$branch' from '$base_branch' at '$worktree_dir'"
  dev::run_git_worktree "$worktree_dir" "$branch" "$base_branch" || return 1

  cd "$worktree_dir" || return 1
  dev::set_tmux_tab "$branch"

  dev::ensure_dir "$vscode_dir"
  dev_mekari::write_vscode_env "$env_file"
  dev_mekari::write_mcp_config "$mcp_file"

  if $open_code && command -v code >/dev/null 2>&1; then
    dev::log "Opening VS Code"
    code .
  elif $open_code; then
    dev::warn "'code' command not found; skipping VS Code launch."
  fi

  dev::log "Worktree ready at $worktree_dir"
}

dev_mekari::write_vscode_env() {
  local env_path=$1

  dev::secret MEKARI_GITHUB_TOKEN "" "MEKARI_GITHUB_TOKEN"
  local github_token=$REPLY
  dev::secret MEKARI_BRAVE_API_KEY "" "MEKARI_BRAVE_API_KEY"
  local brave_token=$REPLY
  dev::secret MEKARI_TAVILY_API_KEY "" "MEKARI_TAVILY_API_KEY"
  local tavily_token=$REPLY
  dev::secret MEKARI_CONFLUENCE_API_TOKEN "" "MEKARI_CONFLUENCE_API_TOKEN"
  local confluence_token=$REPLY
  dev::secret MEKARI_JIRA_API_TOKEN "" "MEKARI_JIRA_API_TOKEN"
  local jira_token=$REPLY
  dev::secret MEKARI_BITBUCKET_APP_PASSWORD "" "MEKARI_BITBUCKET_APP_PASSWORD"
  local bitbucket_password=$REPLY

  local confluence_url=${MEKARI_CONFLUENCE_URL:-"https://jurnal.atlassian.net"}
  local confluence_user=${MEKARI_CONFLUENCE_USERNAME:-"aziz.fikri@mekari.com"}
  local jira_url=${MEKARI_JIRA_URL:-"https://jurnal.atlassian.net"}
  local jira_user=${MEKARI_JIRA_USERNAME:-"aziz.fikri@mekari.com"}
  local bitbucket_user=${MEKARI_BITBUCKET_USERNAME:-"afikrim_mkr"}

  cat > "$env_path" <<EOF
# Autogenerated by dev_mekari::accounting_worktree

GITHUB_PERSONAL_ACCESS_TOKEN=$github_token

BRAVE_API_KEY=$brave_token
TAVILY_API_KEY=$tavily_token

CONFLUENCE_URL=$confluence_url
CONFLUENCE_USERNAME=$confluence_user
CONFLUENCE_API_TOKEN=$confluence_token

JIRA_URL=$jira_url
JIRA_USERNAME=$jira_user
JIRA_API_TOKEN=$jira_token

ATLASSIAN_BITBUCKET_USERNAME=$bitbucket_user
ATLASSIAN_BITBUCKET_APP_PASSWORD=$bitbucket_password
EOF
}

dev_mekari::write_mcp_config() {
  local mcp_path=$1

  cat > "$mcp_path" <<'EOF'
{
  "servers": {
    "context7": {
      "command": "npx",
      "args": ["-y", "@upstash/context7-mcp"],
      "envFile": "${workspaceFolder}/.vscode/.env"
    },
    "omnisearch": {
      "command": "npx",
      "args": ["-y", "mcp-omnisearch"],
      "envFile": "${workspaceFolder}/.vscode/.env"
    },
    "github": {
      "command": "npx",
      "args": ["-y", "@modelcontextprotocol/server-github"],
      "envFile": "${workspaceFolder}/.vscode/.env"
    },
    "bitbucket": {
      "command": "npx",
      "args": ["-y", "@aashari/mcp-server-atlassian-bitbucket"],
      "envFile": "${workspaceFolder}/.vscode/.env"
    }
  }
}
EOF
}

dev_mekari::quickbook_worktree() {
  local branch=$1
  local source=${2:-v_next}

  if [[ -z "$branch" ]]; then
    echo "Usage: gtwqb <new-branch> [source-branch]"
    return 1
  fi

  local dir="$MEKARI_DEV_DIR/$branch"
  dev::log "Creating Quickbook worktree '$branch' from '$source' at '$dir'"
  dev::run_git_worktree "$dir" "$branch" "$source" || return 1

  cd "$dir" || return 1
  dev::set_tmux_tab "$branch"

  dev::log "Restoring files from aziz/local-setupâ€¦"
  local files=(
    .claude/settings.json
    .claude/settings.local.json
    CLAUDE.md
    Gemfile
    Gemfile.lock
    config/application.rb
    config/database.yml
    config/initializers/rack_timeout.rb
    .env
    .python-version
    .ruby-version
  )

  for file in "${files[@]}"; do
    git checkout aziz/local-setup -- "$file" 2>/dev/null || true
  done

  dev::log "Setting Node.js v14 via nvm"
  if command -v nvm >/dev/null 2>&1; then
    nvm install v14
    nvm use v14
  else
    dev::warn "nvm not found; skipping Node.js setup"
  fi

  dev::log "Installing dependencies"
  command -v yarn >/dev/null 2>&1 && yarn install
  command -v bundle >/dev/null 2>&1 && bundle install

  dev::log "Quickbook worktree ready at $dir"
}
